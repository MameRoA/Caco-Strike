ACTOR NightHawk50c : CaSWeapon
{
 Tag "Desert Eagle"
 Weapon.SlotNumber 2
 Weapon.SelectionOrder 2
 Weapon.KickBack 0
 Weapon.AmmoType1 "NightHawkAmmo"
 Weapon.AmmoUse1 1
 Weapon.AmmoType2 "NightHawkReserve"
 Weapon.AmmoUse2 1
 Weapon.AmmoGive2 0
 Decal "BulletChip"
 Obituary "%k >[Desert Eagle]> %o"
 +WEAPON.NOAUTOAIM
 +WEAPON.AMMO_CHECKBOTH
 +WEAPON.NOAUTOFIRE
 +WEAPON.NOLMS
 +INVENTORY.UNDROPPABLE
  States
  {
  Ready:
	TNT1 A 0 A_GiveInventory("PowerSMG",1)
    TNT1 A 0 A_JumpIfInventory("CantFire",1,"NoFire")
    DEAG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
    TNT1 A 0 A_TakeInventory("DeagleRecoil",2)
    Loop
  NoFire:
    TNT1 A 0 A_JumpIfInventory("CantFire",1,1)
	Goto Ready
	DEAG A 1 A_WeaponReady(WRF_NOFIRE)
	Goto Ready
  Deselect: 
    TNT1 A 0 A_TakeInventory("HoldingSecondary",1)
    TNT1 A 0 A_TakeInventory("PowerSMG",1)
    TNT1 AA 0 A_Lower
    DEAG A 1 A_Lower
    Loop
  Select:
    TNT1 A 0 A_GiveInventory("HoldingSecondary",1)
	TNT1 A 0 A_GiveInventory("PowerSMG",1)
    TNT1 AA 0 A_Raise
    DEAG A 1 A_Raise
    Loop
  Fire:
  DEAG A 0 A_JumpIfInventory("NightHawkAmmo",1,1)
  Goto Reload
  DEAF A 0 A_FireCustomMissile("PistolCasing",40+random(-5,5),0,3,10)
  DEAG A 0 A_PlayWeaponSound("weapons/deaglefire")
  DEAG A 0 A_TakeInventory("NightHawkAmmo",1)
  DEAG B 2 bright ACS_NamedExecuteWithResult("DeagleFire")
  TNT1 A 0 A_SetPitch(pitch+frandom(-0.5,-0.8))
  TNT1 A 0 A_SetAngle(angle+frandom(-0.6,0.6))
  DEAG D 3
  DEAG E 3 A_WeaponReady(WRF_ALLOWRELOAD)
  DEAG A 4 A_WeaponReady(WRF_ALLOWRELOAD)
  Goto Ready
  NoAmmo:
  DEAG A 2 A_CheckReload
  Goto Ready
  Reload:
  TNT1 A 0 A_JumpIfInventory("NightHawkReserve", 1, 1)
  Goto NoAmmo
  TNT1 A 0 A_JumpIfInventory("NightHawkAmmo", 0, "Ready")
  DEAR A 0 A_PlayWeaponSound("weapons/deaglere1") 
  DEAR A 3
  DEAR B 2
  DEAR C 18 
  UnitReload:
  TNT1 A 0 A_JumpIfInventory("NightHawkReserve", 1, 1)
  Goto ReloadFinish
  TNT1 A 0 A_JumpIfInventory("NightHawkAmmo", 7, "ReloadFinish")
  TNT1 A 0 A_TakeInventory("NightHawkReserve", 1, TIF_NOTAKEINFINITE)
  TNT1 A 0 A_GiveInventory("NightHawkAmmo", 1)
  Goto UnitReload
  ReloadFinish:
  DEAR A 0 A_PlayWeaponSound("weapons/deaglere2") 
  DEAR DEF 3
  DEAR G 9
  DEAR ECBA 4
  DEAG A 2 A_WeaponReady
  Goto Ready
  Flash:
    TNT1 A 2 bright
    Stop
  }
}
Actor NightHawkReserve : Ammo
{
	Game Doom
	Inventory.Amount 35
	Inventory.MaxAmount 35
	Ammo.BackpackAmount 35
	Ammo.BackpackMaxAmount 35
	+IGNORESKILL
}
Actor NightHawkAmmo : Ammo
{
	Game Doom
	Inventory.Amount 7
	Inventory.MaxAmount 7
	Ammo.BackpackAmount 7
	Ammo.BackpackMaxAmount 7
	+IGNORESKILL
}
Actor DeagleRecoil : Inventory { Inventory.MaxAmount 2 }